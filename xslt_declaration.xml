<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
    xmlns:tei="http://www.tei-c.org/ns/1.0"
    exclude-result-prefixes="tei">

  <xsl:output method="html" encoding="UTF-8" indent="yes"/>

  <xsl:key name="orgById"  match="tei:org"  use="@xml:id"/>
  <xsl:key name="biblById" match="tei:bibl" use="@xml:id"/>

  <xsl:template match="/tei:TEI">
    <html>
      <head>
        <meta charset="UTF-8"/>
        <title>
          <xsl:value-of select="tei:teiHeader/tei:fileDesc/tei:titleStmt/tei:title"/>
        </title>
      </head>
      <body>

        <h1 id="{tei:teiHeader/tei:fileDesc/tei:titleStmt/tei:title/@xml:id}">
          <xsl:value-of select="tei:teiHeader/tei:fileDesc/tei:titleStmt/tei:title"/>
        </h1>

        <xsl:apply-templates select="tei:text/tei:front"/>
        <xsl:apply-templates select="tei:text/tei:body"/>

        <xsl:apply-templates select="
          tei:teiHeader
          /tei:encodingDesc
          /tei:standOff
          /tei:listBibl"/>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="tei:orgName[@ref]">
    <xsl:variable name="oid" select="substring-after(@ref,'#')"/>
    <xsl:variable name="org" select="key('orgById',$oid)"/>
    <a href="{normalize-space($org/@sameAs)}" target="_blank" rel="noopener">
      <xsl:apply-templates/>
    </a>
  </xsl:template>

  <xsl:template match="tei:listBibl">
    <section id="bibliography">
      <h2>Related Documents</h2>
      <ul>
        <xsl:apply-templates select="tei:bibl"/>
      </ul>
    </section>
  </xsl:template>
  <xsl:template match="tei:bibl">
    <li id="{@xml:id}">
      <a href="{normalize-space(@sameAs)}" target="_blank" rel="noopener">
        <strong><xsl:value-of select="tei:title"/></strong>
      </a>
      â€” <xsl:value-of select="@ref"/>
    </li>
  </xsl:template>

  <xsl:template match="tei:front">
    <div class="front">
      <xsl:apply-templates select="*"/>
    </div>
  </xsl:template>

  <xsl:template match="tei:div[@type='preamble']">
    <section class="preamble">
      <h2><xsl:value-of select="tei:head"/></h2>
      <xsl:apply-templates select="tei:p"/>
    </section>
  </xsl:template>

  <xsl:template match="tei:div[@type='article']">
    <section class="article" id="{@n}">
      <h3><xsl:value-of select="tei:head"/></h3>
      <xsl:apply-templates select="tei:p | tei:list"/>
    </section>
  </xsl:template>

  <xsl:template match="tei:p">
    <p><xsl:apply-templates/></p>
  </xsl:template>
  <xsl:template match="tei:list[@type='ordered']">
    <ol><xsl:apply-templates select="tei:item"/></ol>
  </xsl:template>
  <xsl:template match="tei:list[@type='unordered']">
    <ul><xsl:apply-templates select="tei:item"/></ul>
  </xsl:template>
  <xsl:template match="tei:item">
    <li><xsl:apply-templates/></li>
  </xsl:template>

  <xsl:template match="tei:ref[@target]">
    <xsl:variable name="bid" select="substring-after(@target,'#')"/>
    <xsl:variable name="b" select="key('biblById',$bid)"/>
    <a href="{normalize-space($b/@sameAs)}" target="_blank" rel="noopener">
      <xsl:apply-templates/>
    </a>
  </xsl:template>

  <xsl:template match="text()|@*">
    <xsl:copy/>
  </xsl:template>

</xsl:stylesheet>
